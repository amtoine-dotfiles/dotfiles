#!/usr/bin/env nu

$env.WM_TERM = "alacritty"
$env.WM_TERM_FLAGS = "-e"
$env.WM_TERM_SHELL = "nu"

def "connect external monitor" [] {
    let monitors = xrandr | lines | find ' connected' | split column " " name . type | select name type

    let primary = $monitors | where type == primary | get 0.name
    let external = $monitors | where type != primary | get 0.name

    nu --commands $"xrandr --output ($primary) --off --output ($external) --auto"
}

def "bspc configure" [config: record] {
    bspc config border_width $config.border_width
    bspc config window_gap $config.window_gap
    bspc config top_padding $config.padding.top
    bspc config bottom_padding $config.padding.bottom
    bspc config left_padding $config.padding.left
    bspc config right_padding $config.padding.right

    bspc config focused_border_color $config.color.border.focused
    bspc config active_border_color $config.color.border.active
    bspc config presel_feedback_color $config.color.presel_feedback
    bspc config normal_border_color $config.color.border.normal

    $config.windows.rules?.tile? | default [] | each {|name|
        bspc rule -a $name state=tiled focus=on follow=on manage=on
    }
    $config.windows.rules?.chat_clients? | default [] | each {|name|
        bspc rule -a $name desktop=2 state=tiled focus=off follow=off manage=on
    }
    $config.windows.rules?.float? | default [] | each {|name|
        bspc rule -a $name state=floating focus=on follow=on manage=on rectangle=1600x900+160+90
    }
}

let config = try { open ~/.config/bspwm/config.nuon } | default {}

bspc configure $config

connect external monitor
bspc monitor -d 1 2 3

feh --randomize /usr/share/backgrounds/* --bg-fill --no-fehbg

if not (pgrep picom | is-empty) {
    killall picom
}
picom --experimental-backends --daemon

if not (pgrep sxhkd | is-empty) {
    killall sxhkd
}
bash -c "sxhkd -c ~/.config/bspwm/sxhkdrc ~/.config/sxhkd/sxhkdrc &"
